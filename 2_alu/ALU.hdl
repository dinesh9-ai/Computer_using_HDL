// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    Mux16 (a=x,b=false,sel=zx,out=az);
    Mux16 (a=y,b=false,sel=zy,out=bz);
    Not16 (in=az,out=naz);
    Not16 (in=bz,out=nbz);
    Mux16 (a=az,b=naz,sel=nx,out=na);
    Mux16 (a=bz,b=nbz,sel=ny,out=nb);
    And16 (a=na,b=nb,out=aac);
    Add16 (a=na,b=nb,out=bbc);
    Mux16 (b=bbc,a=aac,sel=f,out=ab);
    Not16 (in=ab,out=nab);
    Mux16 (a=ab,b=nab,sel=no,out=fout,out=out);
    And16(a=fout, b=true, out[0..7]=fl, out[8..15]=fh,out[15]=ng);
    Or8Way (in=fl,out=po);
    Or8Way (in=fh,out=fd);
    Or (a=po,b=fd,out=zre);
    Not (in=zre,out=zr);
    
}